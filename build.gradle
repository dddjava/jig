plugins {
    id 'jig.base-conventions'
    id 'io.github.gradle-nexus.publish-plugin' version '2.0.0'
    id 'test-report-aggregation'
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
        }
    }
}

dependencies {
    testReportAggregation project(':jig-core')
    // spring-boot-starterのBOM解決ができないとかでうまく収集できない。一旦コメントアウト。
    //testReportAggregation project(':jig-cli')
    testReportAggregation project(':jig-gradle-plugin')
}

reporting {
    reports {
        testAggregateTestReport(AggregateTestReport) {
            testSuiteName = "unit-test"
        }
    }
}

tasks.register('build') {
    dependsOn tasks.named('testAggregateTestReport', TestReport)
}

subprojects {
    plugins.withType(JavaPlugin).configureEach {
        test {
            reports.html.required = false
        }
    }
}