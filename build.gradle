plugins {
    id 'org.springframework.boot' version '2.0.2.RELEASE' apply false
    id 'com.gradle.plugin-publish' version '0.9.10' apply false
    id 'com.jfrog.bintray' version '1.8.1' apply false
    id 'org.dddjava.jig-gradle-plugin' version '2018.8.3' apply false
}

repositories {
    jcenter()
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'

    version = '2018.8.4'
    group = 'org.dddjava.jig'

    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'

    def defaultEncoding = 'UTF-8'
    tasks.withType(AbstractCompile).each { it.options.encoding = defaultEncoding }

    repositories {
        jcenter()
    }

    dependencies {
        testCompile 'org.junit.jupiter:junit-jupiter-api:5.2.0'
        testCompile 'org.junit.jupiter:junit-jupiter-params:5.2.0'
        testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.2.0'
        testCompile 'org.assertj:assertj-core:3.10.0'
        testCompile 'org.mockito:mockito-core'

        testCompile('org.springframework:spring-test') {
            exclude group: 'junit'
        }
    }

    dependencyManagement {
        imports {
            mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
        }
    }

    test {
        useJUnitPlatform {
            includeEngines 'junit-jupiter'
        }
    }

    javadoc {
        options.addStringOption('Xdoclint:none', '-quiet')
    }

    jar {
        metaInf { from(rootDir) { include 'LICENSE' } }
    }
}

String findSettingValue(String envName, String propertyName) {
    def env = System.getenv().get(envName)
    def property = env ? env : System.properties.getProperty(propertyName)
    property ? property : ''
}

project(':jig-annotation') {
    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.bintray'

    ext {
        artifactId = 'jig-annotation'
    }

    publishing {
        publications {
            maven(MavenPublication) {
                groupId project.group
                artifactId project.artifactId
                version project.version
                from components.java
                pom {
                    name = 'jig-annotation'
                    description = "Annotations of Jig"
                    url = "https://github.com/dddjava/Jig"
                    licenses {
                        license {
                            name = "The Apache Software License, Version 2.0"
                            url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                            distribution = "repo"
                        }
                    }
                    developers {
                        developer {
                            id = "irof"
                            name = "irof"
                            email = "irof@hogedriven.net"
                        }
                        developer {
                            id = "haljik"
                            name = "haljik"
                            email = "seiji.kawakami@sora-works.com"
                        }
                    }
                    scm {
                        url = "https://github.com/dddjava/Jig.git"
                    }
                }
            }
        }
    }

    bintray {
        user = findSettingValue('BINTRAY_USER', 'bintray.user');
        key = findSettingValue('BINTRAY_APIKEY', 'bintray.apikey')

        configurations = ['archives']
        publications = ['maven']
        pkg {
            repo = 'jig'
            name = 'jig-annotation'
            desc = 'Annotations of Jig'
            userOrg = 'dddjava'
            websiteUrl = 'https://github.com/dddjava/Jig'
            vcsUrl = 'https://github.com/dddjava/Jig.git'
            licenses = ["Apache-2.0"]
            publish = true
            publicDownloadNumbers = true
        }
    }

    task sourcesJar(type: Jar, dependsOn:classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives sourcesJar
    }

    build.dependsOn(publishToMavenLocal)
}

project(':jig-core') {
    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.bintray'
    apply plugin: 'org.dddjava.jig-gradle-plugin'

    ext {
        artifactId = 'jig-core'
    }

    publishing {
        publications {
            maven(MavenPublication) {
                groupId project.group
                artifactId project.artifactId
                version project.version
                from components.java
                pom {
                    name = "jig-core"
                    description = "Core of Jig"
                    url = "https://github.com/dddjava/Jig"
                    licenses {
                        license {
                            name = "The Apache Software License, Version 2.0"
                            url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                            distribution = "repo"
                        }
                    }
                    developers {
                        developer {
                            id = "irof"
                            name = "irof"
                            email = "irof@hogedriven.net"
                        }
                        developer {
                            id = "haljik"
                            name = "haljik"
                            email = "seiji.kawakami@sora-works.com"
                        }
                    }
                    scm {
                        url = "https://github.com/dddjava/Jig.git"
                    }
                }
            }
        }
    }

    bintray {
        user = findSettingValue('BINTRAY_USER', 'bintray.user');
        key = findSettingValue('BINTRAY_APIKEY', 'bintray.apikey')

        configurations = ['archives']
        publications = ['maven']
        pkg {
            repo = 'jig'
            name = 'jig-core'
            desc = 'core of Jig'
            userOrg = 'dddjava'
            websiteUrl = 'https://github.com/dddjava/Jig'
            vcsUrl = 'https://github.com/dddjava/Jig.git'
            licenses = ["Apache-2.0"]
            publish = true
            publicDownloadNumbers = true
        }
    }

    task sourcesJar(type: Jar, dependsOn:classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives sourcesJar
    }

    dependencies {
        compile project(':jig-annotation')
        compile 'org.springframework:spring-context'
        compile 'org.slf4j:slf4j-api'
        compile 'ch.qos.logback:logback-classic'

        // バイトコードの読み取り: infrastructure/asm
        compile 'org.ow2.asm:asm:6.1.1'
        // SQLの取得: infrastructure/mybatis
        compile 'org.mybatis:mybatis:3.4.6'
        // javadocコメントの取得: infrastructure/javaparser
        compile 'com.github.javaparser:javaparser-core:3.6.6'
        compile 'com.github.javaparser:java-symbol-solver-core:0.6.3'
        // excel出力: presentation/poi
        compile 'org.apache.poi:poi:3.17'
        compile 'org.apache.poi:poi-ooxml:3.17'
        // ダイアグラム出力: presentation/graphvizj
        compile 'guru.nidi:graphviz-java:0.5.1'

        testCompile 'org.springframework:spring-web'
    }

    build.dependsOn(publishToMavenLocal)
}

project(':jig-cli') {
    apply plugin: 'org.springframework.boot'
    dependencies {
        compile project(':jig-core')
        compile 'org.springframework.boot:spring-boot-starter'
    }
    bootJar {
        metaInf { from(rootDir) { include 'LICENSE' } }
        archiveName 'jig-cli.jar'
    }
}

project(':jig-gradle-plugin') {
    apply plugin: "java-gradle-plugin"
    apply plugin: 'com.gradle.plugin-publish'
    apply plugin: 'maven-publish'

    // Write the plugin's classpath to a file to share with the tests
    task createClasspathManifest {
        def outputDir = file("$buildDir/$name")

        inputs.files sourceSets.main.runtimeClasspath
        outputs.dir outputDir

        doLast {
            outputDir.mkdirs()
            file("$outputDir/plugin-classpath.txt").text = sourceSets.main.runtimeClasspath.join("\n")
        }
    }

    dependencies {
        compile project(':jig-core')
        compile gradleApi()
        compile localGroovy()
        // Add the classpath file to the test runtime classpath
        testRuntime files(createClasspathManifest)
    }

    jar {
        baseName = 'jig-gradle-plugin'
    }

    ext {
        artifactId = 'jig-gradle-plugin'
    }

    gradlePlugin {
        plugins {
            jigGradlePlugin {
                id = "org.dddjava.${project.artifactId}"
                implementationClass = 'org.dddjava.jig.gradle.JigGradlePlugin'
            }
        }
    }



    pluginBundle {
        website = 'https://github.com/dddjava/Jig'
        vcsUrl = 'https://github.com/dddjava/Jig.git'
        description = 'Visualizing code for DDD'
        tags = ['ddd', 'visualize']

        plugins {
            jigGradlePlugin {
                id = "org.dddjava.${project.artifactId}"
                displayName = 'Jig Gradle Plugin'
            }
        }
    }

    publishing {
        publications {
            puluginPublication(MavenPublication) {
                groupId project.group
                artifactId project.artifactId
                version project.version
                from components.java
            }
        }
    }

    task setupPluginUpload << {
        final def KEY_PROPERTY = "gradle.publish.key"
        final def SECRET_PROPERTY = "gradle.publish.secret"

        String key = findSettingValue('GRADLE_PUBLISH_KEY', KEY_PROPERTY)
        String secret = findSettingValue('GRADLE_PUBLISH_SECRET', SECRET_PROPERTY)

        if( key.isEmpty() || secret.isEmpty()) {
            throw new RuntimeException("GRADLE_PUBLISH_KEY and/or GRADLE_PUBLISH_SECRET are not defined environment variables")
        }

        System.properties.setProperty(KEY_PROPERTY, key)
        System.properties.setProperty(SECRET_PROPERTY, secret)
    }

    tasks.publishPlugins.dependsOn tasks.setupPluginUpload
    build.dependsOn(publishToMavenLocal)

    test.dependsOn(createClasspathManifest)

}
