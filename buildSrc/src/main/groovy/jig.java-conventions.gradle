plugins {
    id 'jig.base-conventions'
    id 'java'
}

sourceCompatibility = '17'

project.afterEvaluate {
    def defaultEncoding = 'UTF-8'
    [AbstractCompile, Javadoc].each {
        tasks.withType(it).each { it.options.encoding = defaultEncoding }
    }
}

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:deprecation"
}

tasks.named('jar') {
    metaInf { from(rootDir) { include 'LICENSE' } }
    manifest {
        attributes "Implementation-Version": project.version
    }
}

tasks.named('javadoc') {
    options.addStringOption('Xdoclint:none', '-quiet')
}

repositories {
    mavenCentral()
}

tasks.named('test') {
    useJUnitPlatform()
}

dependencies {
    testImplementation(platform('org.junit:junit-bom:5.12.0'))
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}