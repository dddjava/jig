plugins {
    id 'jig.java-conventions'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = project.group
            artifactId = project.name
            version = project.version
            from components.java
            pom {
                name = "jig-core"
                description = "Core of JIG"
                url = "https://github.com/dddjava/jig"
                licenses {
                    license {
                        name = "The Apache Software License, Version 2.0"
                        url = "https://www.apache.org/licenses/LICENSE-2.0.txt"
                        distribution = "repo"
                    }
                }
                developers {
                    developer {
                        id = "irof"
                        name = "irof"
                        email = "irof.ocean@gmail.com"
                    }
                    developer {
                        id = "haljik"
                        name = "haljik"
                        email = "seiji.kawakami@sora-works.com"
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/dddjava/jig.git'
                    developerConnection = 'scm:git:git://github.com/dddjava/jig.git'
                    url = "https://github.com/dddjava/jig.git"
                }
            }
        }
    }
}

signing {
    def signingKeyId = findProperty("signingKeyId")
    def signingKey = findProperty("signingKey")
    // keyがセットされている時のみ有効（通常のビルドができなくなるため）
    if (signingKeyId && signingKey) {
        def signingPassword = findProperty("signingPassword")
        useInMemoryPgpKeys(signingKeyId, signingKey, signingPassword)
        sign publishing.publications.mavenJava
    }
}

java {
    // MavenCentralへの公開にJavadocとsourceの添付が必要
    withJavadocJar()
    withSourcesJar()
}

dependencies {
    implementation(platform(org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES))

    api "org.slf4j:slf4j-api"

    api libs.asm
    api libs.mybatis
    api libs.javaparser
    api libs.bundles.poi
    api "org.thymeleaf:thymeleaf"
    api "com.github.ben-manes.caffeine:caffeine"
    api "io.micrometer:micrometer-core"
    api "io.micrometer:micrometer-registry-prometheus"
    api "org.jspecify:jspecify"

    testRuntimeOnly("ch.qos.logback:logback-classic")
    testImplementation("org.mockito:mockito-core")
    testImplementation("org.springframework:spring-web")

    // テストで読み取るのに使用するアノテーションなど
    testImplementation("io.swagger.core.v3:swagger-annotations-jakarta:2.2.38")
    testImplementation("jakarta.validation:jakarta.validation-api:3.1.1")
}

build.dependsOn(publishToMavenLocal)
